cmake_minimum_required(VERSION 3.19)

set(vfs_known_comps static shared)
set(vfs_comp_static NO)
set(vfs_comp_shared NO)
foreach (vfs_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (vfs_comp IN_LIST vfs_known_comps)
        set(vfs_comp_${vfs_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "vfs does not recognize component `${vfs_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (vfs_comp_static AND vfs_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "vfs `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(vfs_static_targets "${CMAKE_CURRENT_LIST_DIR}/vfs-static-targets.cmake")
set(vfs_shared_targets "${CMAKE_CURRENT_LIST_DIR}/vfs-shared-targets.cmake")

macro(vfs_load_targets type)
    if (NOT EXISTS "${vfs_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "vfs `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${vfs_${type}_targets}")
endmacro()

if (vfs_comp_static)
    vfs_load_targets(static)
elseif (vfs_comp_shared)
    vfs_load_targets(shared)
elseif (DEFINED vfs_SHARED_LIBS AND vfs_SHARED_LIBS)
    vfs_load_targets(shared)
elseif (DEFINED vfs_SHARED_LIBS AND NOT vfs_SHARED_LIBS)
    vfs_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${vfs_shared_targets}")
        vfs_load_targets(shared)
    else ()
        vfs_load_targets(static)
    endif ()
else ()
    if (EXISTS "${vfs_static_targets}")
        vfs_load_targets(static)
    else ()
        vfs_load_targets(shared)
    endif ()
endif ()